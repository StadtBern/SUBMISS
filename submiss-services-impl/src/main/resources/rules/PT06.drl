package rules.submiss.createDocs;

import ch.bern.submiss.services.api.dto.CompanyDTO;
import ch.bern.submiss.services.api.dto.SubmissionDTO;
import ch.bern.submiss.services.api.dto.SubmissionCancelDTO;
import java.util.HashMap;
import ch.bern.submiss.services.api.util.SubmissConverter;

global HashMap placeholderMappings;

rule "PT06"
	when
		$submission: SubmissionDTO(submissionCancel.empty == false)
		$submissionCancel : SubmissionCancelDTO()
        	from $submission.getSubmissionCancel().get(0);
		$company: CompanyDTO()
	if ($company.getAddress2() != null) do[address2]
	else do[address2Empty]	
	if ($submissionCancel.getAvailableDate() != null) do[availableDate]
	else do[availableDateEmpty]
	if ($submissionCancel.getReason() != null) do[reason]
	else do[reasonEmpty]
	if ($submission.getLegalHearingTerminate().get(0) != null && $submission.getLegalHearingTerminate().get(0).getUpdatedOn() != null) do[exclusionDeadline]
	else do[exclusionDeadlineEmpty]
	then	
		placeholderMappings.put("f_company_name", $company.getCompanyName());
		placeholderMappings.put("f_company_location", $company.getLocation());
		placeholderMappings.put("f_company_post", $company.getPostCode() + " " + $company.getLocation());
	then [address2]
		placeholderMappings.put("f_company_address", $company.getAddress1() + "\\n" + $company.getAddress2());
	then [address2Empty]	
		placeholderMappings.put("f_company_address", $company.getAddress1());
	then [availableDate]	
		placeholderMappings.put("s_cancel_available_date", SubmissConverter.convertToSwissDate($submissionCancel.getAvailableDate()));
	then [availableDateEmpty]	
		placeholderMappings.put("s_cancel_available_date", "");	
	then [reason]	
		placeholderMappings.put("s_cancel_reason", $submissionCancel.getReason());
	then [reasonEmpty]	
		placeholderMappings.put("s_cancel_reason", "");
	then [exclusionDeadline]	
		placeholderMappings.put("r_date", SubmissConverter.convertToSwissDate($submission.getLegalHearingTerminate().get(0).getUpdatedOn()));
	then [exclusionDeadlineEmpty]	
		placeholderMappings.put("r_date", "");					
	end