package rules.submiss.createDocs;

import ch.bern.submiss.services.api.dto.CompanyDTO;
import ch.bern.submiss.services.api.dto.SubmissionDTO;
import ch.bern.submiss.services.api.dto.LegalHearingTerminateDTO;
import java.util.HashMap;
import ch.bern.submiss.services.api.util.SubmissConverter;

global HashMap placeholderMappings;

rule "PT19"
	when
		$company: CompanyDTO()
		$submission: SubmissionDTO(legalHearingTerminate.empty == false)
		$legalHearingTerminate : LegalHearingTerminateDTO()
        	from $submission.getLegalHearingTerminate().get(0);
	if ($company.getAddress2() != null) do[address2]
	else do[address2Empty]
	if ($legalHearingTerminate.getReason() != null) do[reason]
	else do[reasonEmpty]
	if ($legalHearingTerminate.getDeadline() != null) do[deadline]
	else do[deadlineEmpty]	
	then
		placeholderMappings.put("f_company_name", $company.getCompanyName());
		placeholderMappings.put("f_company_post", $company.getPostCode() + " " + $company.getLocation());
	then [address2]
		placeholderMappings.put("f_company_address", $company.getAddress1() + "\\n" + $company.getAddress2());
	then [address2Empty]	
		placeholderMappings.put("f_company_address", $company.getAddress1());
	then [reason]	
		placeholderMappings.put("r_cancel_reason", $legalHearingTerminate.getReason());
	then [reasonEmpty]	
		placeholderMappings.put("r_cancel_reason", "");	
	then [deadline]	
		placeholderMappings.put("r_cancel_deadline", SubmissConverter.convertToSwissDate($legalHearingTerminate.getDeadline()));
		placeholderMappings.put("r_date", SubmissConverter.convertToSwissDate($legalHearingTerminate.getDeadline()));
	then [deadlineEmpty]	
		placeholderMappings.put("r_cancel_deadline", "");	
		placeholderMappings.put("r_date", "");			
	end